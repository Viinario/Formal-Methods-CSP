-------------------------------------------------------------------------------
-- Tipos e constantes
-------------------------------------------------------------------------------

datatype NOMES = N1 | N2 | N3

datatype TELEFONES = T1 | T2 | T3

MAX = 3

EXEMPLO_AGENDA = {(N1,T1), (N2,T2), (N3, T3)}

nametype CONTATOS = (NOMES,TELEFONES) ---> nameType que representa um contato (NOME, TELEFONE)
	
-------------------------------------------------------------------------------
-- Funções
-------------------------------------------------------------------------------

-- nas funcoes a seguir, contatos representa um conjunto com tuplas
-- na forma (n,t) onde n é do tipo NOMES e t é do tipo TELEFONES

-- retorna um conjunto com todos os nomes do conjunto
nomes(contatos) = { n | (n,t) <- contatos }

-- retorna o conjunto dos telefones associados a um contato
tels(contatos, n) = { t | (n_,t) <- contatos, n_ == n }

-- retorna o conjunto com um novo contato adicionado
adi(contatos, n, t) = union(contatos,{(n,t)})
	
-- remove do conjunto o contato cujo n e fornecido
rem(contatos, n) = { (n_,t) | (n_,t) <- contatos, n_ != n }

-- atualiza no conjunto o numero t_atual de um contato para t_novo
mod(contatos, n, t_atual, t_novo) = 
	let
		outros = { (n_,t) | (n_,t) <- contatos, n_ != n, t != t_atual }
	within
		union(outros, {(n,t_novo)})
		

-- retorna o conjunto de numeros que nao estao associados a um contato
nao_associados(contatos, n) = { t | t <- TELEFONES, not member(t,tels(contatos,n)) }

-- retorna o conjunto de numeros que sao diferentes do numero atual do contato
novos(contatos, n, t_atual) = { t | t <- TELEFONES, t != t_atual, not member(t, tels(contatos,n))  }

-- une duas agendas, representadas como conjuntos de contatos
unir_agendas(contatos1, contatos2) = union(contatos1, contatos2)

-------------------------------------------------------------------------------
-- Canais e processos
-------------------------------------------------------------------------------

channel inserir : NOMES.TELEFONES
channel consultar : NOMES.Set(TELEFONES)
channel remover : NOMES
channel modificar : NOMES.TELEFONES.TELEFONES
channel migrar: Set(CONTATOS) --> Set de contatos {(Nome,telefone)...}

AGENDA = Pagenda({})

-- insere apenas telefones que nao estao associados a um contato
-- consulta apenas os telefones dos contatos cadastrados
-- remove contatos cadastrados
-- modifica contatos cadastrados de forma que o novo numero seja diferente do atual
Pagenda(contatos) = 
	card(contatos) < MAX & 
	 inserir?n?t:nao_associados(contatos, n) -> Pagenda(adi(contatos,n,t))
	[]
	consultar?n:nomes(contatos)!tels(contatos,n) -> Pagenda(contatos)
	[]
	remover?n:nomes(contatos) -> Pagenda(rem(contatos,n))
	[]
	modificar?n:nomes(contatos)
			?t_atual:tels(contatos,n)
			?t_novo:novos(contatos, n, t_atual) -> 
			Pagenda(mod(contatos,n,t_atual,t_novo))
	[]
	card(contatos) < MAX & -- verifica se é possivel adicionar na agenda atual (Limite de Memoria)
		migrar?a ->
			if card(a) <= MAX then -- verifica se a agenda escolhida é valida para realizar o merge
			    if card(unir_agendas(a, contatos)) <= MAX then -- Verifica se o merge não cria uma agenda maior que o limite difinido pelo MAX
					Pagenda(unir_agendas(a, contatos)) -- Faz o merge das agendas
				else
					Pagenda(contatos) -- Não faz o merge das agendas (DEFINA COMO STOP para ver no :probe AGENDA melhor)
			else
				Pagenda(contatos) -- Não faz o merge das agendas (DEFINA COMO STOP para ver no :probe AGENDA melhor)